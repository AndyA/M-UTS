!to "m-uts.rom", plain

* = $8000

!addr osfind = $ffce
!addr osgbpb = $ffd1
!addr osbput = $ffd4
!addr osbget = $ffd7
!addr osargs = $ffda
!addr osfile = $ffdd
!addr osrdch = $ffe0
!addr osasci = $ffe3
!addr osnewl = $ffe7
!addr oswrch = $ffee
!addr osword = $fff1
!addr osbyte = $fff4
!addr oscli = $fff7
!addr irq2v = $206
!addr irq1v = $204
!addr brkv = $202
!addr userv = $200
!addr uptv = $222
!addr evntv = $220
!addr fscv = $21e
!addr findv = $21c
!addr gbpbv = $21a
!addr bputv = $218
!addr bgetv = $216
!addr argsv = $214
!addr filev = $212
!addr rdchv = $210
!addr wrchv = $20e
!addr wordv = $20c
!addr bytev = $20a
!addr cliv = $208

!addr ptr0_lo = $a8
!addr ptr0_hi = $a9

!addr acc0_lo = $aa
!addr acc0_hi = $ab
!addr acc1_lo = $ac
!addr acc1_hi = $ad

!addr cmd_offset = $ae

!addr cmdjmp_lo = $b0
!addr cmdjmp_hi = $b1
!addr cmdhelp_lo = $b2
!addr cmdhelp_hi = $b3

!addr acc2_lo = $b4
!addr acc2_hi = $b5

!addr cmdline_lo = $f2
!addr cmdline_hi = $f3
!addr cur_rom = $f4
!addr err_lo = $fd
!addr err_hi = $fe
!addr esc_flag = $ff

!addr vars = $300

!addr regs = $400
!addr reg_a = regs + ('A' - 'A') * 2
!addr reg_i = regs + ('I' - 'A') * 2
!addr reg_x = regs + ('X' - 'A') * 2
!addr reg_y = regs + ('Y' - 'A') * 2
!addr reg_p = regs + ('P' - 'A') * 2
!addr reg_s = regs + ('S' - 'A') * 2


!addr breakpoints_lo = $500
!addr breakpoints_hi = $501
!addr breakpoints_save = $502
!addr breakpoints_huh = $503

!addr brk_cmd = $7a0

!addr rom_private = $df0

!addr x_present = $a000
!addr x_assemble = $a001
!addr x_eval = $a004

!macro inc16 addr {
  inc addr
  bne +
  inc addr + 1
+
}

!macro adc16 dst, lhs, rhs {
  lda lhs
  adc rhs
  sta dst
  lda lhs + 1
  adc rhs + 1
  sta dst + 1
}

!macro mnem c1, c2, c3, bits, mask {
  !set .cc = ((c1 - $3f) << 11) | ((c2 - $3f) << 6) | ((c3 - $3f) << 1)
  !byte < .cc, > .cc, bits, mask
}

L8000               jmp language
                    
L8003               jmp service


                    ; ROM type
rom_type            !byte %11000010
                    ; Copyright offset. Points to 0 before copyright
copyright_offset    !byte copyright - L8000 - 1
                    ; Version
                    !byte 2 
title               !text "M-UTS", 0
version             !text "1.60", 0
copyright           !text "(C) AIAA 1984", 0

; Jump table to expose well known addresses to optional upper half of ROM

j_prstr             jmp prstr
j_throw             jmp throw
j_newline           jmp newline
j_check_escape      jmp check_escape
j_hexbyte           jmp hexbyte
j_hexbyte_space     jmp hexbyte_space
j_radix_out         jmp radix_out
j_getch             jmp getch
j_skip_space        jmp skip_space
j_eval              jmp eval
j_eval_byte         jmp eval_byte
j_check_eol         jmp check_eol
j_parse_bytestring  jmp parse_bytes
j_try_poke          jmp try_poke
j_acc0_to_tmp0      jmp acc0_to_acc2
j_unget             jmp unget
j_x0                jmp 0
j_x1                jmp 0
                    
cmd_table           !text "ASSEMBL", 'E' + $80
                    !word c_assemble, h_assemble 
                    !text "BAS", 'E' + $80
                    !word c_base, h_base 
                    !text "CAL", 'C' + $80
                    !word c_calc, h_calc 
                    !text "CHANG", 'E' + $80
                    !word c_change, h_change 
                    !text "CL", 'R' + $80
                    !word c_clr, h_clr 
                    !text "DI", 'S' + $80
                    !word c_dis, h_dis 
                    !text "EDI", 'T' + $80
                    !word c_edit, h_edit 
                    !text "FLIS", 'T' + $80
                    !word c_flist, h_flist 
                    !text "FOR", 'M' + $80
                    !word c_form, h_form 
                    !text "FIN", 'D' + $80
                    !word c_find, h_find 
                    !text "GE", 'T' + $80
                    !word c_get, h_get 
                    !text "G", 'O' + $80
                    !word c_go, h_go 
                    !text "LOO", 'K' + $80
                    !word c_look, h_look 
                    !text "ME", 'M' + $80
                    !word c_mem, h_mem 
                    !text "MOD", 'E' + $80
                    !word c_mode, h_mode 
                    !text "MONITO", 'R' + $80
                    !word c_monitor, h_monitor 
                    !text "MOV", 'E' + $80
                    !word c_move, h_move 
                    !text "ONBR", 'K' + $80
                    !word c_onbrk, h_onbrk 
                    !text "PU", 'T' + $80
                    !word c_put, h_put 
                    !text "REG", 'S' + $80
                    !word c_regs, h_regs 
                    !text "SAM", 'E' + $80
                    !word c_same, h_same 
                    !text "SELEC", 'T' + $80
                    !word c_select, h_select 
                    !text "SE", 'T' + $80
                    !word c_set, h_set 
                    !text "STE", 'P' + $80
                    !word c_step, h_step 
                    !text "VD", 'U' + $80
                    !word c_vdu, h_vdu 
                    !text "VERIF", 'Y' + $80
                    !word c_verify, h_verify 
                    !text "WHER", 'E' + $80
                    !word c_where, h_where 

                    ; Command table also used for operator names

                    ; AND
op_and              !text "AN", 'D' + $80
                    !word 0, 0 
                    ; OR
                    !text "O", 'R' + $80
                    !word 0, 0 
                    ; EOR
                    !text "EO", 'R' + $80
                    !word 0, 0 
                    ; NOT
                    !text "NO", 'T' + $80
                    !word 0, 0 
                    !text 0

help_table                    
h_assemble          !text "<fsp> <fsp", '>' + $80
h_base              !text "<addr", '>' + $80
h_calc              !text "<expr", '>' + $80
h_change            !text "<addr> <byte string", '>' + $80
h_clr               !text "(<addr>", ')' + $80
h_dis               !text "<strt>(..<end>) (H<prefix>"
                    !text ") (P,L", ')' + $80
h_edit              !text "<addr", '>' + $80
h_flist             !text "(<number>", ')' + $80
h_form              !text "<tracks> (<drv>", ')' + $80
h_find              !text "<strt>(..<end>) <byte string", '>' + $80
h_get               !text "<track> (<drv>", ')' + $80
h_go                !text "<addr> (<A>) (<X>) (<Y>", ')' + $80
h_look              !text "<addr> (H<prefix>", ')' + $80
h_mem               !text "<strt>(..<end>) (P,L", ')' + $80
h_mode              !text "<mode", '>' + $80
h_monitor           !text ' ' + $80
h_move              !text "(>) <src> <dest> <len> (<addr>", ')' + $80
h_onbrk             !text "<command line", '>' + $80
h_put               !text "<track> (<drv>", ')' + $80
h_regs              !text ' ' + $80
h_same              !text "<addr> <addr> <len", '>' + $80
h_select            !text "<rom number", '>' + $80
h_set               !text "<addr", '>' + $80
h_step              !text "<addr> (H<prefix>) (P,L", ')' + $80
h_vdu               !text "<byte string", '>' + $80
h_verify            !text "(<drv>", ')' + $80
h_where             !text ' ' + $80
                    !text 0
                    
c_assemble          lda x_present
                    cmp #$ff
                    bne throw_noasm
                    jmp x_assemble
                    
throw_noasm         jsr throw
                    !text $a0, "No assembler", 0

dispatch_cmd        lda cmdline_lo
                    pha
                    lda cmdline_hi
                    pha
                    jsr lookup_command
                    bpl L82fc
                    jsr getch
                    and #$df
                    cmp #'A'
                    bcc +
                    cmp #'Z' + 1
                    bcs +
L82fc               pla
                    sta cmdline_hi
                    pla
                    sta cmdline_lo
                    sec
                    rts
                    
+                   jsr unget
                    lda cmdjmp_lo
                    ora cmdjmp_hi
                    beq L82fc
                    pla
                    pla
                    jsr call_cmd
                    clc
                    rts
                    
call_cmd            jmp (cmdjmp_lo)
                    
pr_muts             jsr prstr
                    !text "M-UTS 1.60", 13, 0
                    rts
                    
pr_banner           jsr newline
                    jsr pr_muts
                    jsr prstr
                    !text -2, "MONITOR", 13, 0
                    rts
                    
oswrch_counted      inc acc2_lo
                    jmp oswrch
                    
show_help           jsr newline
                    jsr pr_muts
                    jsr newline
                    ldx #0
                    ldy #0
                    lda #< help_table
                    sta acc0_lo
                    lda #> help_table
                    sta acc0_hi
L8355               lda #$02
                    sta acc2_lo
                    tya
                    pha
                    jsr prstr
                    !text -2, 0
L8360               pla
                    tay
L8362               lda cmd_table, y
                    iny
                    pha
                    and #$7f
                    jsr oswrch_counted
                    pla
                    bpl L8362
                    iny
                    iny
                    iny
                    iny
                    lda #' '
                    jsr oswrch_counted
L8378               lda (acc0_lo, x)
                    +inc16 acc0_lo
                    pha
                    and #$7f
                    jsr oswrch_counted
                    pla
                    bpl L8378
                    jsr check_escape
                    ; pad with spaces to 40 cols
                    lda #' '
                    ldx acc2_lo
L8390               jsr oswrch_counted
                    inx
                    cpx #40
                    bcc L8390
                    ldx #0
                    lda (acc0_lo, x)
                    bne L8355
                    jmp newline
                    
prstr               pla
                    sta ptr0_lo
                    pla
                    sta ptr0_hi
                    ldy #0
L83a9               +inc16 ptr0_lo
                    lda (ptr0_lo), y
                    beq L83c5
                    bpl L83c0
                    tax
                    lda #' '
L83b8               jsr oswrch
                    inx
                    bne L83b8
                    beq L83a9
L83c0               jsr osasci
                    bne L83a9
L83c5               lda ptr0_hi
                    pha
                    lda ptr0_lo
                    pha
                    rts
                    
throw               jsr reset_terminal
                    pla
                    sta ptr0_lo
                    pla
                    sta ptr0_hi
                    ldy #0
                    sty $0100
L83da               iny
                    lda (ptr0_lo), y
                    sta $0100, y
                    bne L83da
                    jmp $0100
                    
newline             jsr osnewl
check_escape        bit esc_flag
                    bmi throw_escape
                    rts
                    
throw_escape        jsr throw
                    ; "Escape"
                    !text 17, "Escape", 0
                    
hexbyte             pha
                    ror
                    ror
                    ror
                    ror
                    jsr hexnybble
                    pla
hexnybble           pha
                    and #$0f
                    cmp #10
                    sed
                    adc #'0'
                    cld
L840a               jsr oswrch
                    pla
                    rts
                    
hexbyte_space       jsr hexbyte
                    pha
                    lda #' '
                    bne L840a
                    
radix_out           sta acc1_lo
                    lda #0
                    pha
L841c               lda #0
                    ldy #16
L8420               asl acc0_lo
                    rol acc0_hi
                    rol
                    cmp acc1_lo
                    bcc L842d
                    sbc acc1_lo
                    inc acc0_lo
L842d               dey
                    bne L8420
                    cmp #10
                    bcc L8436
                    adc #'A' - '9' - 2
L8436               adc #'0'
                    pha
                    dex
                    lda acc0_lo
                    ora acc0_hi
                    bne L841c
                    lda #' '
L8442               dex
                    bmi L844a
                    jsr oswrch
                    bne L8442
L844a               pla
                    jsr oswrch
                    bne L844a
                    rts
                    
getch               ldy #0
                    lda (cmdline_lo), y
                    +inc16 cmdline_lo
                    rts
                    
skip_space          jsr getch
                    cmp #' '
                    beq skip_space
                    cmp #13
                    rts
                    
to_upper            pha
                    and #$df
                    cmp #'A'
                    bcc L8475
                    cmp #'Z' + 1
                    bcs L8475
                    pla
                    and #$df
                    rts
L8475               pla
                    rts
                    
radix_parse         sta acc1_lo
                    lda ptr0_lo
                    pha
                    lda ptr0_hi
                    pha
                    lda #0
                    sta acc0_lo
                    sta acc0_hi
                    jsr skip_space
L8488               jsr to_upper
                    sec
                    sbc #'0'
                    bcc L84d9
                    cmp #10
                    bcc L849a
                    cmp #'A' - '9' - 1 + 10
                    bcc L84d9
                    sbc #'A' - '9' - 1
L849a               cmp acc1_lo
                    bcs L84d9
                    pha
                    lda acc0_lo
                    sta ptr0_lo
                    lda acc0_hi
                    sta ptr0_hi
                    lda #0
                    sta acc0_lo
                    sta acc0_hi
                    ldx #5
                    lda acc1_lo
L84b1               ror
                    bcc L84c3
                    pha
                    clc
                    +adc16 acc0_lo, acc0_lo, ptr0_lo
                    pla
L84c3               asl ptr0_lo
                    rol ptr0_hi
                    dex
                    bne L84b1
                    clc
                    pla
                    adc acc0_lo
                    sta acc0_lo
                    bcc L84d4
                    inc acc0_hi
L84d4               lda (cmdline_lo), y
                    iny
                    bne L8488
L84d9               pla
                    sta ptr0_hi
                    pla
                    sta ptr0_lo
                    tya
                    clc
                    adc cmdline_lo
                    sta cmdline_lo
                    bcc L84e9
                    inc cmdline_hi
L84e9               jsr unget
                    dey
                    rts
                    
eval_atom           tsx           ; check SP
                    cpx #$80
                    bcs L850b
                    jsr throw
                    !text $a1, "Formula too complex", 0
                    
L850b               jsr lookup_token
                    ; - = negate term
                    cmp #'-'
                    bne L8523
                    jsr eval_atom
                    lda #0
                    sec
                    sbc acc0_lo
                    sta acc0_lo
                    lda #0
                    sbc acc0_hi
                    sta acc0_hi
                    rts
                    
                    ; + = skipped
L8523               cmp #'+'
                    beq eval_atom
                    ; ? = peek
                    cmp #'?'
                    bne L8537
                    jsr eval_atom
                    ldy #0
                    lda (acc0_lo), y
                    sta acc0_lo
                    sty acc0_hi
                    rts
                    
                    ; ! = word peek
L8537               cmp #'!'
                    bne L854b
                    jsr eval_atom
                    ldy #0
                    lda (acc0_lo), y
                    tax
                    iny
                    lda (acc0_lo), y
                    sta acc0_hi
                    stx acc0_lo
                    rts
                    
                    ; @ = set radix
L854b               cmp #'@'
                    bne L8568
                    lda #$0a
                    sta acc1_lo
                    jsr eval_d
                    lda acc0_lo
                    cmp #$02
                    bcc L857c
                    cmp #$23
                    bcs L857c
                    sta acc1_lo
                    jsr skip_comma
                    jmp eval_atom
                    
                    ; 9e = NOT
L8568               cmp #$9e
                    bne L8589
                    jsr eval_atom
                    lda acc0_lo
                    eor #$ff
                    sta acc0_lo
                    lda acc0_hi
                    eor #$ff
                    sta acc0_hi
L857b               rts
                    
L857c               jsr throw
                    !text $a2, "Bad base", 0
                    
                    ; ( = nested
L8589               cmp #'('
                    beq L8595
                    ; { = nested
                    cmp #'{'
                    beq L8595
                    ; [ = nested
                    cmp #'['
                    bne L85bb
L8595               cmp #'[' - 1
                    adc #1
                    pha
                    jsr eval_hex
                    jsr skip_space
                    sta ptr0_lo
                    pla
                    cmp ptr0_lo
                    beq L857b
                    jsr throw
                    !text $a3, "Missing bracket", 0
                    ; ' = char const
L85bb               cmp #"'"
                    bne L85ce
                    jsr getch
                    sta acc0_lo
                    sty acc0_hi
                    jsr getch
                    cmp #"'"
                    bne L8629
                    rts
                    
                    ; : = variable lookup
L85ce               cmp #':'
                    bne L85fd
                    jsr getch
                    and #$df
                    cmp #'A'
                    bcs L85ec
throw_badvar        jsr throw
                    !text $a4, "Bad variable", 0
L85ec               cmp #'Z' + 1
                    bcs throw_badvar
                    asl
                    tax
                    lda vars + '?' * 2, x
                    sta acc0_lo
                    lda vars + '?' * 2 + 1, x
                    sta acc0_hi
                    rts
                    
                    ; extended rom?
L85fd               ldx x_present
                    cpx #$ff
                    bne L8607
                    jmp x_eval
                    
L8607               jsr unget
                    lda acc1_lo
                    jsr radix_parse
                    bmi L8629
                    jsr skip_space
                    jsr to_upper
                    ; k suffix?
                    cmp #'K'
                    bne L8626
                    ; multiply by 1024
                    lda acc0_lo
                    asl
                    asl
                    sta acc0_hi
                    lda #0
                    sta acc0_lo
                    rts
                    
L8626               jmp unget
                    
L8629               jmp syntax_error
                    
eval_b              jsr eval_atom
                    jsr skip_space
                    ; * = multiply
                    cmp #'*'
                    bne L866a
                    lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval_b
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    lda #0
                    sta ptr0_lo
                    sta ptr0_hi
                    ldy #$10
L864d               lsr acc0_hi
                    ror acc0_lo
                    bcc L8660
                    clc
                    +adc16 ptr0_lo, acc2_lo, ptr0_lo
L8660               asl acc2_lo
                    rol acc2_hi
                    dey
                    bne L864d
                    jmp L867f
                    
                    ; / = divide
L866a               cmp #'/'
                    bne L8678
                    jsr divmod
                    lda acc2_lo
                    ldx acc2_hi
                    jmp L8683
                    
                    ; % = mod
L8678               cmp #'%'
                    bne L8688
                    jsr divmod
L867f               lda ptr0_lo
                    ldx ptr0_hi
L8683               sta acc0_lo
                    stx acc0_hi
                    rts
                    
L8688               jmp unget
                    
eval_c              jsr eval_b
                    jsr skip_space
                    ; + = add
                    cmp #'+'
                    bne L86aa
                    lda acc0_hi
                    pha
                    lda acc0_lo
                    pha
                    jsr eval_c
                    clc
                    pla
                    adc acc0_lo
                    sta acc0_lo
                    pla
                    adc acc0_hi
                    sta acc0_hi
                    rts
                    
                    ; - = subtract
L86aa               cmp #'-'
                    bne L86c3
                    lda acc0_hi
                    pha
                    lda acc0_lo
                    pha
                    jsr eval_c
                    sec
                    pla
                    sbc acc0_lo
                    sta acc0_lo
                    pla
                    sbc acc0_hi
                    sta acc0_hi
                    rts
                    
L86c3               jmp unget
                    
divmod              lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval_b
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    ldy #16
                    lda #0
                    sta ptr0_lo
                    sta ptr0_hi
L86dd               asl acc2_lo
                    rol acc2_hi
                    rol ptr0_lo
                    rol ptr0_hi
                    lda ptr0_lo
                    cmp acc0_lo
                    lda ptr0_hi
                    sbc acc0_hi
                    bcc L86fd
                    lda ptr0_lo
                    sbc acc0_lo
                    sta ptr0_lo
                    lda ptr0_hi
                    sbc acc0_hi
                    sta ptr0_hi
                    inc acc2_lo
L86fd               dey
                    bne L86dd
                    rts
                    
eval_hex            lda #16
                    sta acc1_lo
eval_d              jsr eval_c
                    jsr lookup_token
                    ; AND
                    cmp #$9b
                    bne L8723
                    lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval_d
                    pla
                    and acc0_hi
                    sta acc0_hi
                    pla
                    and acc0_lo
                    sta acc0_lo
                    rts
                    
                    ; OR
L8723               cmp #$9c
                    bne L873b
                    lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval_d
                    pla
                    ora acc0_hi
                    sta acc0_hi
                    pla
                    ora acc0_lo
                    sta acc0_lo
                    rts
                    
                    ; EOR
L873b               cmp #$9d
                    bne unget
                    lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval_d
                    pla
                    eor acc0_hi
                    sta acc0_hi
                    pla
                    eor acc0_lo
                    sta acc0_lo
                    rts
                    
unget               lda cmdline_hi
                    bne L8759
                    dec cmdline_hi
L8759               dec cmdline_lo
                    rts
                    
eval                php
                    pha
                    tya
                    pha
                    txa
                    pha
                    lda ptr0_lo
                    pha
                    lda ptr0_hi
                    pha
                    lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    jsr eval_hex
                    jsr skip_comma
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    pla
                    sta ptr0_hi
                    pla
                    sta ptr0_lo
                    pla
                    tax
                    pla
                    tay
                    pla
                    plp
                    rts
                    
eval_byte           lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    jsr eval
                    ldy acc0_lo
                    lda acc0_hi
                    beq L87ac
                    jsr throw
                    ; "Byte out of range"
                    !text $a5, "Byte out of range", 0
L87ac               pla
                    sta acc0_hi
                    pla
                    sta acc0_lo
                    tya
                    rts
                    
skip_comma          jsr skip_space
                    cmp #','
                    beq L87be
                    jmp unget
                    
L87be               rts
                    
lookup_token        lda cmd_offset
                    pha
                    lda cmdhelp_lo
                    pha
                    lda cmdhelp_hi
                    pha
                    jsr lookup_command
                    tax
                    pla
                    sta cmdhelp_hi
                    pla
                    sta cmdhelp_lo
                    pla
                    sta cmd_offset
                    txa
                    rts
                    
lookup_command      jsr skip_space
                    jsr unget
                    lda #$80
                    sta acc1_hi
                    ldx #0
lc_next_cmd         ldy #$ff
                    stx cmd_offset
lc_next_char        iny
                    lda (cmdline_lo), y
                    and #$df
                    sta ptr0_lo
                    lda cmd_table, x
                    php
                    inx
                    and #$7f
                    cmp ptr0_lo
                    bne lc_dot
                    plp
                    bpl lc_next_char
lc_match            iny
                    tya
                    ldy #0
                    clc
                    adc cmdline_lo
                    sta cmdline_lo
                    bcc L8809
                    inc cmdline_hi
L8809               lda cmd_table, x
                    sta cmdjmp_lo, y
                    inx
                    iny
                    cpy #4
                    bcc L8809
                    lda acc1_hi
                    rts
                    
                    ; check for . abbreviation of command
lc_dot              plp
                    inc acc1_hi
                    dex
                    dex
L881d               inx
                    lda cmd_table, x
                    bpl L881d
                    inx
                    lda (cmdline_lo), y
                    cmp #'.'
                    beq lc_match
                    inx
                    inx
                    inx
                    inx
                    lda cmd_table, x
                    bne lc_next_cmd
                    jmp getch
                    
                    ; "Syntax: "
syntax_prefix       !text "Syntax: "
check_eol           jsr skip_space
                    bne syntax_error
                    rts
                    
syntax_error        ldx #0
                    stx $0100
                    lda acc1_hi
                    sta $0101
                    ldy cmd_offset
                    cpy #$ff
                    bne L8867
                    jsr throw
                    ; "Bad expression"
                    !text $a6, "Bad expression", 0
L8867               lda syntax_prefix, x
                    sta $0102, x
                    inx
                    cpx #$08
                    bcc L8867
L8872               lda cmd_table, y
                    php
                    and #$7f
                    sta $0102, x
                    iny
                    inx
                    plp
                    bpl L8872
                    lda #' '
                    sta $0102, x
                    ldy #0
L8887               lda (cmdhelp_lo), y
                    php
                    and #$7f
                    sta $0103, x
                    iny
                    inx
                    plp
                    bpl L8887

S8894               lda #0
                    sta $0103, x
                    jmp $0100
                    
parse_bytes         ldx #0
parse_bytes_tail    jsr skip_comma
                    jsr skip_space
                    cmp #$22
                    bne L88c0
L88a8               jsr getch
                    cmp #$22
                    bne L88b6
                    jsr getch
                    cmp #$22
                    bne L88c0
L88b6               cmp #13
                    beq L88e4
                    jsr try_poke
                    jmp L88a8
                    
L88c0               jsr unget
                    jsr skip_space

                    ; $ = peek?
                    cmp #'$'
                    beq L88e7
                    jsr unget
                    jsr skip_space
                    cmp #13
L88d2               bne L88d5
                    rts
                    
L88d5               jsr unget
                    jsr skip_comma
                    jsr eval_byte
                    jsr try_poke
                    jmp parse_bytes_tail
                    
L88e4               jmp syntax_error
                    
L88e7               jsr eval
                    ldy #0
L88ec               lda (acc0_lo), y
                    iny
                    cmp #13
                    beq parse_bytes_tail
                    sty acc1_lo
                    jsr try_poke
                    ldy acc1_lo
                    jmp L88ec
                    
try_poke            ldy #0
                    sta (ptr0_lo), y
                    cmp (ptr0_lo), y
                    beq L891a
                    jsr throw
                    ; "Read only memory"
                    !text $a7, "Read only memory", 0
L891a               +inc16 ptr0_lo
                    inx
                    rts
                    
acc0_to_acc2        lda acc0_lo
                    sta acc2_lo
                    lda acc0_hi
                    sta acc2_hi
                    rts
                    
c_find              jsr parse_range
                    ldx #0
                    stx ptr0_lo
                    inx
                    stx ptr0_hi
                    jsr parse_bytes
                    stx cmdline_lo
L893a               ldy #0
                    ldx #0
L893e               lda $0100, x
                    cmp (acc2_lo), y
                    bne L8958
                    iny
                    inx
                    cpx cmdline_lo
                    bcc L893e
                    lda acc2_hi
                    jsr hexbyte
                    lda acc2_lo
                    jsr hexbyte_space
                    jsr check_escape
L8958               inc acc2_lo
                    bne L895e
                    inc acc2_hi
L895e               lda acc2_lo
                    cmp acc0_lo
                    lda acc2_hi
                    sbc acc0_hi
                    bcc L893a
                    jmp newline
                    
c_change            jsr eval
                    lda acc0_lo
                    sta ptr0_lo
                    lda acc0_hi
                    sta ptr0_hi
                    jmp parse_bytes
                    
print_or_page       jsr skip_space
                    beq L89a0
                    cmp #','
                    beq print_or_page
                    and #$df
                    cmp #'P'
                    bne L898f
                    lda #14
                    jsr oswrch
                    bne print_or_page
L898f               cmp #'L'
                    bne L8999
                    jsr enable_printer
                    jmp print_or_page
                    
L8999               cmp #13
                    beq L89a0
                    jmp syntax_error
                    
L89a0               rts
                    
                    ; flush buffers
enable_printer      lda #$0f
                    ldx #0
                    ldy #0
                    jsr osbyte
                    lda #$02
                    jsr oswrch
                    jsr skip_space
                    cmp #'*'
                    beq L89a0
                    jsr unget
                    lda #' '
                    jsr oswrch
                    lda #13
                    jsr oswrch
                    lda #3
                    jsr oswrch
                    lda #$80
                    dey
                    ldx #$fc
                    jsr osbyte
                    cpx #$3f
                    bne throw_noprinter
                    lda #2
                    jmp oswrch
                    
throw_noprinter     jsr throw
                    ; "No printer"
                    !text $a8, "No printer", 0
                    
parse_range         jsr eval
                    jsr acc0_to_acc2
                    jsr skip_space
                    cmp #'.'
                    bne L89ff
                    jsr skip_space
                    cmp #'.'
                    beq L8a09
                    jmp syntax_error
                    
L89ff               jsr unget
                    ; $ffff = end of memory
                    lda #$ff
                    sta acc0_lo
                    sta acc0_hi
                    rts
                    
L8a09               jmp eval
                    
reset_terminal      jsr prstr
                    ; disable printer, page mode off
                    !text 3, 15, 0
L8a12               rts
                    
calc_columns        jsr osnewl
                    lda #8
                    jsr oswrch
                    lda #$86
                    jsr osbyte
                    jsr newline
                    lda #16
                    cpx #80 - 1
                    beq L8a2a
                    lsr
L8a2a               rts
                    
ram_warning         txa
                    pha
                    tya
                    pha
                    jsr prstr
                    !text "RAM from ", 0
L8a3c               pla
                    jsr hexbyte
                    tya
                    jsr hexbyte_space
                    jsr prstr
                    !text "to ", 0
L8a4b               pla
                    jsr hexbyte
                    tya
                    jsr hexbyte_space
L8a53               jsr prstr
                    !text "will be corrupted", 13, 0
ask_continue        jsr prstr
                    !text "continue (Y/N) ? ", 0
L8a7e               jsr osrdch
                    bcc L8a86
                    jmp throw_escape
                    
L8a86               and #$df
                    cmp #'Y'
                    beq L8a8e
                    lda #'N'
L8a8e               jsr oswrch
                    pha
                    jsr newline
                    pla
                    lsr
L8a97               rts
                    
c_monitor           jsr skip_space
                    cmp #13
                    beq L8ace
                    jsr unget
                    lda cmdline_lo
                    sta $0100
                    lda cmdline_hi
                    sta $0101
                    lda #$83
                    jsr osbyte
                    stx $0102
                    sty $0103
                    lda #$82
                    jsr osbyte
                    stx $0104
                    sty $0105
                    ldx #0
                    stx $0106
                    ldy #$01
                    lda #$ff
                    jsr osfile
L8ace               ldx cur_rom
                    lda #$8e
                    jmp osbyte
                    
                    ; address
readline_parms      !word $0700 
                    ; length
                    !byte 160 - 2
                    ; min char
                    !byte ' '
                    ; max char
                    !byte $ff

empty               !text 13, 0, 0, 0
                    
                    ; ROM service entry
language            cmp #1
                    bne L8a97
                    lda #< brk_handler
                    sta brkv
                    lda #> brk_handler
                    sta brkv + 1
                    lda #0
                    ldx #$3f
                    sta $0600
                    sta $0601
                    sta $0602
L8af9               sta breakpoints_lo, x
                    dex
                    bpl L8af9
                    ldx #$04
L8b01               lda empty, x
                    sta brk_cmd, x
                    dex
                    bpl L8b01
                    inx
                    stx $0604
                    lda #$f0
                    sta reg_s
                    lda #$5d
                    sta $0603
                    cli
                    cld
                    jmp repl
                    
brk_handler         txa
                    pha
                    ldx #0
                    lda err_lo
                    sec
                    sbc #1
                    sta acc0_lo
                    lda err_hi
                    sbc #0
                    sta acc0_hi
L8b2e               lda acc0_lo
                    cmp breakpoints_lo, x
                    bne L8b3f
                    lda acc0_hi
                    cmp breakpoints_hi, x
                    bne L8b3f
                    jmp bp_handler
                    
L8b3f               inx
                    inx
                    inx
                    inx
                    cpx #$40
                    bcc L8b2e
                    lda #$7e
                    jsr osbyte
                    jsr osnewl
                    ldy #$01
L8b51               lda (err_lo), y
                    beq L8b63
                    iny
                    cmp #' '
                    bcc L8b51
                    cmp #$7f
                    beq L8b51
                    jsr oswrch
                    bne L8b51
L8b63               cli
                    cld
                    ldx #$ff
                    txs
                    jsr osnewl
                    
                    ; start of repl loop
repl                jsr set_private
                    ldx #0
L8b70               lda $0603, x
                    inx
                    jsr oswrch
                    bne L8b70
                    sta cmdline_lo
                    ; readline_parms
                    ldx #< readline_parms
                    ldy #> readline_parms
                    jsr osword
                    bcc L8b87
                    jmp throw_escape
                    
L8b87               ldx #$07
                    stx cmdline_hi
                    jsr skip_space
                    beq repl
                    ; :<var> =
                    cmp #':'
                    bne L8bd2
                    jsr skip_space
                    and #$df
                    cmp #'A'
                    bcs L8ba0
L8b9d               jmp throw_badvar
                    
L8ba0               cmp #'Z' + 1
                    bcs L8b9d
                    asl
                    tax
                    ldy #$ff
                    sty cmd_offset
                    jsr skip_space
                    cmp #$3d
                    beq L8bbf
throw_missingeq     jsr throw
                    !text $a9, "Missing =", 0
L8bbf               jsr eval
                    jsr check_eol
                    lda acc0_lo
                    sta vars + '?' * 2, x
                    lda acc0_hi
                    sta vars + '?' * 2 + 1, x
                    jmp repl
                    
                    ; / = set prompt
L8bd2               cmp #'/'
                    bne L8bed
                    ldx #0
L8bd8               jsr getch
                    cmp #$0d
                    beq L8be5
                    sta $0603, x
                    inx
                    bne L8bd8
L8be5               lda #0
                    sta $0603, x
                    jmp repl
                    
                    ; ? = show help
L8bed               cmp #'?'
                    bne L8bf7
                    jsr show_help
                    jmp repl
                    
                    ; $<addr> = bytestring
L8bf7               cmp #'$'
                    bne L8c18
                    jsr eval
                    lda acc0_lo
                    sta ptr0_lo
                    lda acc0_hi
                    sta ptr0_hi
                    jsr skip_space
                    cmp #$3d
                    bne throw_missingeq
                    jsr parse_bytes
                    lda #$0d
                    jsr try_poke
                    jmp repl
                    
                    ; attempt command
L8c18               jsr unget
                    jsr dispatch_cmd
                    bcs L8c23
                    jmp repl
                    
                    ; fallback to oscli
L8c23               ldx #0
                    ldy #$07
                    jsr oscli
                    jmp repl
                    
bp_handler          pla
                    sta reg_x
                    lda $fc
                    sta reg_a
                    sty reg_y
                    pla
                    sta reg_p
                    jsr prstr
                    !text "Break: PC = ", 0
L8c4d               lda acc0_hi
                    jsr hexbyte
                    lda acc0_lo
                    jsr hexbyte
                    jsr newline
                    lda #$a0
                    sta cmdline_lo
                    jmp L8b87
                    
do_calc             jsr eval
                    jsr check_eol
                    ldy #2
                    ldx #18
                    jsr radix_print
                    ldy #8
                    ldx #8
                    jsr radix_print
                    ldy #10
                    ldx #7
                    jsr radix_print
                    ldy #16
                    ldx #6
                    jsr radix_print
                    lda $0402
                    cmp #$02
                    bcc L8cc6
                    cmp #$24
                    bcs L8cc6
                    ldx cur_rom
                    ldy rom_private, x
                    bpl L8cc6
                    pha
                    jsr prstr
                    !text 13, "Base ", 0
L8ca0               pla
                    pha
                    tay
                    lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    ldx #0
                    stx acc0_hi
                    sty acc0_lo
                    ldy #10
                    jsr radix_print
                    pla
                    sta acc0_hi
                    pla
                    sta acc0_lo
                    jsr prstr
                    !text ": ", 0
L8cc0               pla
                    tay
                    ldx #0
                    beq radix_print
L8cc6               rts
                    
radix_print         lda acc0_lo
                    pha
                    lda acc0_hi
                    pha
                    tya
                    jsr radix_out
                    pla
                    sta acc0_hi
                    pla
                    sta acc0_lo
                    rts
                    
c_calc              jsr do_calc
                    jmp newline
                    
c_move              lda #0
                    sta $0100
                    jsr skip_space
                    cmp #'>'
                    bne L8cef
                    sta $0100
                    beq L8cf2
L8cef               jsr unget
L8cf2               jsr eval
                    jsr acc0_to_acc2
                    jsr eval
                    lda #$83
                    jsr osbyte
                    txa
                    cmp acc0_lo
                    tya
                    sbc acc0_hi
                    bcc L8d35
                    lda $0100
                    bne L8d35
                    txa
                    pha
                    tya
                    pha
                    jsr prstr
                    !text "Destination <= ", 0
L8d24               pla
                    jsr hexbyte
                    pla
                    jsr hexbyte
                    jsr newline
                    jsr ask_continue
                    bcs L8d35
                    rts
                    
L8d35               lda acc0_lo
                    sta ptr0_lo
                    lda acc0_hi
                    sta ptr0_hi
                    jsr eval
                    lda $0100
                    bne L8d48
                    jsr check_eol
L8d48               ldy #0
                    lda ptr0_lo
                    cmp acc2_lo
                    lda ptr0_hi
                    sbc acc2_hi
                    bcc L8d8e
                    clc
                    +adc16 acc2_lo, acc2_lo, acc0_lo
                    clc
                    +adc16 ptr0_lo, ptr0_lo, acc0_lo
                    +inc16 acc0_lo
L8d74               lda (acc2_lo), y
                    sta (ptr0_lo), y
                    lda acc2_lo
                    bne L8d7e
                    dec acc2_hi
L8d7e               dec acc2_lo
                    lda ptr0_lo
                    bne L8d86
                    dec ptr0_hi
L8d86               dec ptr0_lo
                    jsr countdown
                    bne L8d74
                    rts
                    
L8d8e               lda (acc2_lo), y
                    jsr try_poke
                    +inc16 acc2_lo
                    jsr countdown
                    bne L8d8e
                    lda $0100
                    beq L8dac
                    jsr eval
                    jsr check_eol
                    jmp (acc0_lo)
                    
L8dac               rts
                    
countdown           lda acc0_lo
                    bne L8db3
                    dec acc0_hi
L8db3               dec acc0_lo
                    lda acc0_lo
                    ora acc0_hi
                    rts
                    
set_private         lda #$ff
                    bne +

clr_private         lda #0
+                   ldx cur_rom
                    sta rom_private, x
                    rts
                    
get_private         ldx cur_rom
                    lda rom_private, x
L8dcb               rts
                    
check_cmd_mode      jsr get_private
                    bmi L8dcb
                    jsr throw
                    ; "Command mode only"
                    !text $aa, "Command mode only", 0

addr_modes          
addr_zp             !byte a_zp    - addr_modes
addr_zpi            !byte a_zpi   - addr_modes
addr_abs            !byte a_abs   - addr_modes
addr_absix          !byte a_absix - addr_modes
addr_absy           !byte a_absy  - addr_modes
addr_absi           !byte a_absi  - addr_modes
addr_zpiy           !byte a_zpiy  - addr_modes
addr_zpix           !byte a_zpix  - addr_modes
addr_a              !byte a_a     - addr_modes
addr_impl           !byte a_impl  - addr_modes
addr_imm            !byte a_imm   - addr_modes
addr_rel            !byte a_rel   - addr_modes

a_zp                !text "H1", 0
a_zpi               !text "H1,I", 0
a_abs               !text "2", 0
a_absix             !text "2,I", 0
a_absy              !text "2,Y", 0
a_absi              !text "(2)", 0
a_zpiy              !text "(H1),Y", 0
a_zpix              !text "(H1,X)", 0
a_a                 !text "A", 0
a_impl              !text " ", 0
a_imm               !text "#H1", 0
a_rel               !text "H+", 0
                    !text 13, 12

                    ; first two bytes are 5 bits-per-char encodings of mnemonic
                    ; second two bytes are mask and bit pattern


op_decode           +mnem 'B', 'C', 'C', $90, $ff
                    +mnem 'B', 'C', 'S', $b0, $ff
                    +mnem 'B', 'E', 'Q', $f0, $ff
                    +mnem 'B', 'M', 'I', $30, $ff
                    +mnem 'B', 'N', 'E', $d0, $ff
                    +mnem 'B', 'P', 'L', $10, $ff
                    +mnem 'B', 'R', 'K', $00, $ff
                    +mnem 'B', 'V', 'C', $50, $ff
                    +mnem 'B', 'V', 'S', $70, $ff
                    +mnem 'C', 'L', 'C', $18, $ff
                    +mnem 'C', 'L', 'D', $d8, $ff
                    +mnem 'C', 'L', 'I', $58, $ff
                    +mnem 'C', 'L', 'V', $b8, $ff
                    +mnem 'D', 'E', 'X', $ca, $ff
                    +mnem 'D', 'E', 'Y', $88, $ff
                    +mnem 'I', 'N', 'X', $e8, $ff
                    +mnem 'I', 'N', 'Y', $c8, $ff
                    +mnem 'J', 'S', 'R', $20, $ff
                    +mnem 'N', 'O', 'P', $ea, $ff
                    +mnem 'P', 'H', 'A', $48, $ff
                    +mnem 'P', 'H', 'P', $08, $ff
                    +mnem 'P', 'L', 'A', $68, $ff
                    +mnem 'P', 'L', 'P', $28, $ff
                    +mnem 'R', 'T', 'I', $40, $ff
                    +mnem 'R', 'T', 'S', $60, $ff
                    +mnem 'S', 'E', 'C', $38, $ff
                    +mnem 'S', 'E', 'D', $f8, $ff
                    +mnem 'S', 'E', 'I', $78, $ff
                    +mnem 'T', 'A', 'X', $aa, $ff
                    +mnem 'T', 'A', 'Y', $a8, $ff
                    +mnem 'T', 'S', 'X', $ba, $ff
                    +mnem 'T', 'X', 'A', $8a, $ff
                    +mnem 'T', 'X', 'S', $9a, $ff
                    +mnem 'T', 'Y', 'A', $98, $ff
                    +mnem 'B', 'I', 'T', $24, $f7
                    +mnem 'J', 'M', 'P', $4c, $df
                    +mnem 'C', 'P', 'X', $e0, $f3
                    +mnem 'C', 'P', 'Y', $c0, $f3
                    +mnem 'S', 'T', 'X', $86, $e7
                    +mnem 'S', 'T', 'Y', $84, $e7
                    +mnem 'D', 'E', 'C', $c6, $e7
                    +mnem 'I', 'N', 'C', $e6, $e7
                    +mnem 'A', 'S', 'L', $02, $e3
                    +mnem 'L', 'S', 'R', $42, $e3
                    +mnem 'R', 'O', 'L', $22, $e3
                    +mnem 'R', 'O', 'R', $62, $e3
                    +mnem 'L', 'D', 'X', $a2, $e3
                    +mnem 'L', 'D', 'Y', $a0, $e3
                    +mnem 'S', 'T', 'A', $81, $e3
                    +mnem 'A', 'D', 'C', $61, $e3
                    +mnem 'A', 'N', 'D', $21, $e3
                    +mnem 'C', 'M', 'P', $c1, $e3
                    +mnem 'E', 'O', 'R', $41, $e3
                    +mnem 'L', 'D', 'A', $a1, $e3
                    +mnem 'O', 'R', 'A', $01, $e3
                    +mnem 'S', 'B', 'C', $e1, $e3
                    !byte 0, 0, 0, 0
op_decode_end

                    !byte 0, 0

                    ; Maps opcode, mask to offset index in
                    ; addr_modes. Would be more efficient
                    ; to map directly to offset
addr_decode         !byte $20, $ff, addr_abs   - addr_modes
                    !byte $10, $1f, addr_rel   - addr_modes 
                    !byte $09, $1f, addr_imm   - addr_modes
                    !byte $0a, $9f, addr_a     - addr_modes
                    !byte $08, $0d, addr_impl  - addr_modes
                    !byte $00, $9f, addr_impl  - addr_modes
                    !byte $4c, $ff, addr_abs   - addr_modes
                    !byte $80, $9d, addr_imm   - addr_modes
                    !byte $84, $9d, addr_zp    - addr_modes
                    !byte $8c, $9d, addr_abs   - addr_modes
                    !byte $94, $9d, addr_zpi   - addr_modes
                    !byte $9c, $9d, addr_absix - addr_modes
                    !byte $6c, $ff, addr_absi  - addr_modes
                    !byte $00, $1c, addr_zpix  - addr_modes
                    !byte $04, $1c, addr_zp    - addr_modes
                    !byte $0c, $1c, addr_abs   - addr_modes
                    !byte $10, $1c, addr_zpiy  - addr_modes
                    !byte $14, $1c, addr_zpi   - addr_modes
                    !byte $18, $1c, addr_absy  - addr_modes
                    !byte $1c, $1c, addr_absix - addr_modes
addr_decode_end

                    ; Illegal opcodes
bad_op              !byte $04, $0c, $12, $14, $1a, $1c, $22, $32
                    !byte $34, $3a, $3c, $42, $44, $52, $54, $5a
                    !byte $5c, $62, $64, $72, $74, $7a, $7c, $80
                    !byte $82, $89, $92, $9c, $9e, $b2, $c2, $d2
                    !byte $d4, $da, $dc, $e2, $f2, $f4, $fa, $fc
bad_op_end

L8f6e               jsr S8894
                    lda #$0d
peek                ldx #0
                    stx $b6
read_rom            php
                    sei
                    tya
                    pha
                    lda acc2_lo
                    sta $f6
                    lda acc2_hi
                    sta $f7
                    jsr get_private
                    bmi L8f8b
                    txa
                    bpl L8f8e
L8f8b               lda $0602
L8f8e               tay
                    jsr $ffb9
                    +inc16 acc2_lo
                    pha
                    inc $b6
                    lda $b6
                    and #$07
                    tax
                    pla
                    sta $010a, x
                    tax
                    pla
                    tay
                    txa
                    plp
                    rts
                    
dump_line           txa
                    pha
                    lda acc2_hi
                    pha
                    jsr hexbyte
                    lda acc2_lo
                    pha
                    jsr hexbyte_space
L8fb8               txa
                    pha
                    jsr peek
                    jsr hexbyte_space
                    pla
                    tax
                    dex
                    bne L8fb8
                    pla
                    sta acc2_lo
                    pla
                    sta acc2_hi
                    pla
                    tax
L8fcd               txa
                    pha
                    jsr peek
                    jsr safe_ascii
                    pla
                    tax
                    dex
                    bne L8fcd
                    rts
                    
L8fdb               lda #'.'
safe_ascii          cmp #' '
                    bcc L8fdb
                    cmp #$7f
                    bcs L8fdb
                    jmp oswrch
                    
c_mem               jsr parse_range
                    jsr calc_columns
                    pha
                    jsr print_or_page
L8ff2               pla
                    pha
                    tax
                    jsr dump_line
                    jsr newline
                    lda acc2_lo
                    cmp acc0_lo
                    lda acc2_hi
                    sbc acc0_hi
                    bcc L8ff2
                    pla
                    jmp reset_terminal
                    
decode_admode       ldy #0
                    sty $b6
                    jsr read_rom
L9010               pha
                    and addr_decode + 1, y
                    cmp addr_decode, y
                    beq L9022
                    pla
                    iny
                    iny
                    iny
                    cpy #addr_decode_end - addr_decode + 2
                    bcc L9010
                    pha
L9022               pla
                    lda addr_decode + 2, y
                    cmp #$0c
                    bcs L9093
                    ldx $b7
                    beq L9093
                    tay
                    lda #' '
                    jsr oswrch
                    lda addr_modes, y 
                    tay
L9038               lda addr_modes, y 
                    beq L9093
                    iny

                    cmp #'1'
                    bne L904b
                    jsr read_rom
                    jsr hexbyte
                    jmp L9038
                    
L904b               cmp #'2'
                    bne L9055
                    jsr pr_sym16
                    jmp L9038
                    
L9055               cmp #'+'
                    bne L9081
                    jsr read_rom
                    tax
                    lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    txa
                    php
                    ldx #0
                    plp
                    bpl L906b
                    dex
L906b               clc
                    adc acc2_lo
                    sta acc2_lo
                    txa
                    adc acc2_hi
                    sta acc2_hi
                    jsr pr_addr
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    jmp L9038
                    
L9081               cmp #'I'
                    bne L9087
                    lda $b7

L9087               cmp #'H'
                    bne L908d
                    lda $b8
L908d               jsr oswrch
                    jmp L9038
                    
L9093               rts
                    
pr_addr             clc
                    lda acc2_lo
                    adc ptr0_lo
                    pha
                    lda acc2_hi
                    adc ptr0_hi
                    jsr hexbyte
                    pla
                    jmp hexbyte
                    
decode_instr        lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    lda #'X'
                    sta $b7
                    jsr read_rom

                    ; illegal op?
                    ldx #0
L90b4               cmp bad_op, x
                    beq L90c5
                    inx
                    cpx #bad_op_end - bad_op
                    bcc L90b4
                    tax
                    and #$03
                    cmp #$03
                    bne L90cb
L90c5               ldx #$0b ; indicate bad op
                    lda #0
                    sta $b7

                    ; lookup op
L90cb               txa
                    ldx #0
L90ce               pha
                    and op_decode + 3, x
                    cmp op_decode + 2, x
                    beq L90e1
                    pla
                    inx
                    inx
                    inx
                    inx
                    cpx #op_decode_end - op_decode
                    bcc L90ce
                    pha
L90e1               pla     ; found match

                    ; stash packed mnemonic
                    lda op_decode, x
                    sta $0100
                    lda op_decode + 1, x
                    sta $0101

                    ; decode 2 bytes -> 3 chars
                    ldy #$03
L90f0               ldx #$05
                    lda #0
L90f4               asl $0100
                    rol $0101
                    rol
                    dex
                    bne L90f4
                    clc
                    adc #'?'
                    jsr oswrch
                    cmp #'X'
                    bne L910a
                    inc $b7
L910a               dey
                    bne L90f0
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    rts
                    
dis_op              jsr pr_addr
                    lda #' '
                    jsr oswrch
                    jsr decode_instr
                    jsr decode_admode
                    lda #$86
                    jsr osbyte
                    lda #' '
L9129               jsr oswrch
                    inx
                    cpx #19
                    bcc L9129
                    ldy #0
L9133               lda $010b, y
                    jsr hexbyte_space
                    iny
                    cpy $b6
                    bcc L9133
                    lda #$86
                    jsr osbyte
                    lda #' '
L9145               jsr oswrch
                    inx
                    cpx #$1d
                    bcc L9145
                    ldy #0
L914f               lda $010b, y
                    jsr safe_ascii
                    iny
                    cpy $b6
                    bcc L914f
                    jmp newline
                    
c_base              jsr check_cmd_mode
                    jsr eval
                    jsr check_eol
                    lda acc0_lo
                    sta $0600
                    lda acc0_hi
                    sta $0601
                    rts
                    
copyright_marker    !text 0, "(C)"
                    
c_select            jsr check_cmd_mode
                    jsr eval_byte
                    pha
                    jsr check_eol
                    pla
                    cmp #$10
                    bcc L9190
L9184               jsr throw
                    !text $ab, "Bad ROM", 0
L9190               sta $0602
                    lda #< copyright_offset
                    sta acc2_lo
                    lda #> copyright_offset
                    sta acc2_hi
                    jsr read_rom
                    sta acc2_lo
                    ldx #$04
L91a2               txa
                    pha
                    jsr read_rom
                    tay
                    pla
                    tax
                    cpy copyright_marker   ; seems dodgy
                    bne L9184
                    inx
                    cpx #$04
                    bcc L91a2
                    lda #$09
                    sta acc2_lo
                    jsr prstr
                    ; "Rom: "
                    !text "Rom: ", 0
L91c1               jsr read_rom
                    jsr oswrch
                    bne L91c1
                    jmp newline
                    
c_dis               jsr parse_range
                    jsr get_private
                    bmi L91db
L91d4               lda acc2_lo
                    ldx acc2_hi
                    jmp L91e9
                    
L91db               lda $0600
                    ora $0601
                    beq L91d4
                    lda $0600
                    ldx $0601
L91e9               sta ptr0_lo
                    stx ptr0_hi
                    sec
                    lda ptr0_lo
                    sbc acc2_lo
                    sta ptr0_lo
                    lda ptr0_hi
                    sbc acc2_hi
                    sta ptr0_hi
                    jsr hex_prefix
                    jsr print_or_page
L9200               jsr dis_op
                    lda acc2_lo
                    cmp acc0_lo
                    lda acc2_hi
                    sbc acc0_hi
                    bcc L9200
                    jmp reset_terminal
                    
hex_prefix          jsr skip_space
                    cmp #'H'
                    bne L921c
                    jsr skip_space
                    bne L9221
L921c               jsr unget
                    lda #'&'
L9221               sta $b8
                    rts
                    
service             php
                    pha
                    txa
                    pha
                    tya
                    pha
                    lda cmdline_lo
                    pha
                    lda cmdline_hi
                    pha
                    clc
                    tya
                    adc cmdline_lo
                    sta cmdline_lo
                    bcc L923a
                    inc cmdline_hi
L923a               tsx
                    lda $0105, x
                    ; star command not recognised
                    cmp #4
                    beq star_command
                    ; *help
                    cmp #9
                    beq help
service_done        pla
                    sta cmdline_hi
                    pla
                    sta cmdline_lo
                    pla
                    tay
service_exit        pla
                    tax
                    pla
                    plp
                    rts
                    
help                jsr skip_space
                    php
                    jsr unget
                    plp
                    beq L926a
                    jsr lookup_command
                    cmp #$8f
                    bne L926d
                    jsr show_help
                    jmp L9278
                    
L926a               jsr pr_banner
L926d               jmp service_done
                    
star_command        jsr clr_private
                    jsr dispatch_cmd
                    bcs L9285
L9278               tsx
                    lda #0
                    sta $0105, x
                    pla
                    pla
                    pla
                    ldy #0
                    beq service_exit
L9285               jmp service_done
                    
regnames            !text "AXYS"
flagnames           !text "nv.bdizc"
                    
show_regs           jsr check_eol
                    ldx #0
L9299               lda regnames, x
                    pha
                    jsr oswrch
                    jsr prstr
                    !text " = ", 0
                    
L92a7               pla
                    asl
                    tay
                    lda vars + '?' * 2, y
                    jsr hexbyte_space
                    inx
                    cpx #$04
                    bcc L9299
                    jsr prstr
                    !text 13, "P = ", 0
                    
show_flags          ldy #0
                    lda reg_p
L92c3               ldx #'.'
                    asl
                    bcc L92cb
                    ldx flagnames, y
L92cb               pha
                    txa
                    jsr oswrch
                    pla
                    iny
                    cpy #8
                    bcc L92c3
                    rts
                    
c_regs              jsr show_regs
                    jmp newline
                    
c_go                jsr check_cmd_mode
                    jsr eval
                    ldx #0
L92e5               jsr skip_space
                    beq L9305
                    jsr unget
                    lda regnames, x
                    asl
                    sta $0100
                    jsr eval_byte
                    ldy $0100
                    sta vars + '?' * 2, y
                    inx
                    cpx #$04
                    bcc L92e5
                    jmp syntax_error
                    
L9305               php
                    jsr load_regs
                    jsr go_acc
                    jsr save_regs
                    plp
                    jmp newline
                    
go_acc              jmp (acc0_lo)
                    
load_regs           lda reg_p
                    pha
                    lda reg_a
                    ldx reg_x
                    ldy reg_y
                    plp
                    rts
                    
save_regs           php
                    sta reg_a
                    stx reg_x
                    sty reg_y
                    pla
                    sta reg_p
                    rts
                    
c_where             jsr check_cmd_mode
                    jsr check_eol
                    jsr prstr
                    ; "Breakpoints:"
                    !text "Breakpoints:", 13, 0
                    ldx #0
L934d               lda breakpoints_lo, x
                    ora breakpoints_hi, x
                    beq L9361
                    lda breakpoints_hi, x
                    jsr hexbyte
                    lda breakpoints_lo, x
                    jsr hexbyte_space
L9361               inx
                    inx
                    inx
                    inx
                    cpx #$40
                    bcc L934d
                    jmp newline
                    
c_set               jsr check_cmd_mode
                    jsr eval
                    jsr check_eol
                    ldx #0
                    lda acc0_lo
                    sta ptr0_lo
                    lda acc0_hi
                    sta ptr0_hi
L937f               lda breakpoints_lo, x
                    ora breakpoints_hi, x
                    beq L939b
                    inx
                    inx
                    inx
                    inx
                    cpx #$40
                    bcc L937f
                    jsr throw
                    !text $ac, "No room", 0
L939b               txa
                    pha
                    lda (acc0_lo), y
                    sta breakpoints_save, x
                    tya
                    jsr try_poke
                    pla
                    tax
                    lda acc0_lo
                    sta breakpoints_lo, x
                    lda acc0_hi
                    sta breakpoints_hi, x
                    rts
                    
c_clr               jsr check_cmd_mode
                    jsr skip_space
                    beq L9402
                    jsr unget
                    jsr eval
                    jsr check_eol
                    ldx #0
L93c6               lda acc0_lo
                    cmp breakpoints_lo, x
                    bne L93e1
                    lda acc0_hi
                    cmp breakpoints_hi, x
                    bne L93e1
                    lda breakpoints_save, x
                    sta (acc0_lo), y
                    tya
                    sta breakpoints_lo, x
                    sta breakpoints_hi, x
                    rts
                    
L93e1               inx
                    inx
                    inx
                    inx
                    cpx #$40
                    bcc L93c6
                    jsr throw
                    !text $ad, "Breakpoint not found", 0
L9402               ldx #0
                    ldy #0
L9406               lda breakpoints_lo, x
                    ora breakpoints_hi, x
                    beq L9424
                    lda breakpoints_lo, x
                    sta acc0_lo
                    lda breakpoints_hi, x
                    sta acc0_hi
                    lda breakpoints_save, x
                    sta (acc0_lo), y
                    tya
                    sta breakpoints_lo, x
                    sta breakpoints_hi, x
L9424               inx
                    inx
                    inx
                    inx
                    cpx #$40
                    bcc L9406
                    rts
                    
c_onbrk             jsr check_cmd_mode
                    ldx #0
L9432               jsr getch
                    sta brk_cmd, x
                    inx
                    cmp #$0d
                    bne L9432
                    rts
                    
get_put_parms       sta $0100
                    stx $0109
                    sty $0106
                    lda acc0_lo
                    sta $0107
                    lda acc0_hi
                    sta $0108
                    ; page
                    lda #$83
                    jsr osbyte
                    stx $0101
                    sty $0102
                    ; machine address
                    lda #$82
                    jsr osbyte
                    stx $0103
                    sty $0104
                    lda #$03
                    sta $0105
                    lda #$7f
                    ldx #0
                    ldy #$01
                    rts
                    
get_drive           lda #$01
                    sta $0100
                    ; machine high order address
                    lda #$82
                    jsr osbyte
                    stx $0103
                    sty $0104
                    lda #$08
                    sta $0101
                    ldy #$01
                    sty $0102
                    lda #$06
                    ldx #0
                    jsr osgbpb
                    lda $0109
                    and #$03
                    rts
                    
c_verify            jsr get_drive
                    pha
                    jsr skip_space
                    bne L94a8
                    pla
                    pha
                    jmp L94c5
                    
L94a8               jsr unget
                    jsr eval_byte
                    pha
                    jsr check_eol
                    pla
                    cmp #$04
                    bcc L94c5

throw_baddrive      jsr throw
                    ; "Bad drive"
                    !text $ae, "Bad drive", 0

L94c5               jsr star_drive
                    sta ptr0_lo
                    lda #0
                    sta acc0_lo
                    sta acc0_hi
                    jsr disk_size
L94d3               lda acc0_lo
                    jsr hexbyte
                    lda ptr0_lo
                    ldx #$2a
                    ldy #$5f
                    jsr get_put_parms
                    jsr osword
                    lda #' '
                    ldx $010a
                    beq L94ed
                    lda #'?'
L94ed               jsr oswrch
                    lda #' '
                    jsr oswrch
                    inc acc0_lo
                    sec
                    lda acc2_lo
                    sbc #10
                    sta acc2_lo
                    bcs L9502
                    dec acc2_hi
L9502               ora acc2_hi
                    bne L94d3
                    pla
                    jmp star_drive
                    
disk_size           lda cmdhelp_hi
                    pha
                    lda $b6
                    pha
                    ldx #$b3
                    ldy #0
                    lda #$7e
                    jsr osword
                    pla
                    sta $b6
                    pla
                    sta cmdhelp_hi
                    rts
                    
drive_cmd           !text "DRIVE "
drive_cmd_end

star_drive          pha
                    ldx #0
L9529               lda drive_cmd, x
                    sta $010a, x
                    inx
                    cpx #drive_cmd_end - drive_cmd
                    bcc L9529
                    pla
                    ora #'0'
                    sta $0110
                    lda #13
                    sta $0111
                    ldx #$0a
                    ldy #$01
                    jmp oscli
                    
show_fkeys          ldy #0
L9548               jsr show_fkey
                    iny
                    cpy #16
                    bcc L9548
                    rts
                    
c_flist             jsr skip_space
                    beq show_fkeys
                    jsr unget
                    jsr eval_byte
                    cmp #16
                    bcc L956c
                    jsr throw
                    !text $fb, "Bad key", 0
L956c               tay
show_fkey           sty acc0_lo
                    jsr prstr
                    !text "*KEY", 0
L9577               jsr check_escape
                    ldx #0
L957c               stx acc0_hi
                    ldy #10
                    jsr radix_print
                    lda acc0_lo
                    ora #$80
                    tay
                    ldx #0
                    lda #$8a
                    jsr osbyte
                    jsr prstr
                    !text -2, 0
-                   lda #4
                    ldx #2
                    jsr osbyte
                    lda #$81
                    ldy #0
L959f               ldx #0
                    jsr osbyte
                    cpy #0
                    bne L95af
                    txa
                    jsr esc_char
                    jmp -
                    
L95af               lda #4
                    ldx #0
                    jsr osbyte
                    ldy acc0_lo
                    jmp newline
                    
esc_char            ldx #'|'
                    cmp #$80
                    bcc +
                    pha
                    txa
                    jsr oswrch
                    lda #'!'
                    jsr oswrch
                    pla
                    and #$7f
+                   cmp #' '
                    bcs +
                    pha
                    txa
                    jsr oswrch
                    pla
                    ora #'@'
+                   cmp #'|'
                    bne +
                    jsr oswrch
+                   cmp #$7f
                    bne +
                    txa
                    jsr oswrch
                    lda #'?'
+                   jmp oswrch
                    
times3              sta $0100
                    asl
                    clc
                    adc $0100
                    rts
                    
div3                sta $0100
                    ldx #8
                    lda #0
-                   asl $0100
                    rol
                    cmp #3
                    bcc +
                    sbc #3
                    inc $0100
+                   dex
                    bne -
                    sta $0101
                    rts
                    
show_line           lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    jsr cursor_off
                    lda #13
                    jsr oswrch
                    ldx acc0_hi
                    jsr dump_line
                    lda #$86
                    jsr osbyte
                    lda #31
                    jsr oswrch
                    lda acc0_lo
                    jsr oswrch
                    tya
                    jsr oswrch
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    jmp cursor_on
                    
c_edit              jsr eval
                    jsr acc0_to_acc2
                    jsr check_eol
                    jsr prstr
                    ; restore default windows, clear screen
                    !text 26, 12, 0
L9650               lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    jsr calc_columns
                    sta acc0_hi
                    ldy #0
                    sty ptr0_hi
                    ldx #0
                    ldy #0
                    cmp #8
                    beq L9669
                    ldy #layout_80 - layout_40
L9669               lda layout_40, y
                    sta cmdjmp_lo, x
                    iny
                    inx
                    cpx #layout_80 - layout_40
                    bcc L9669
L9674               ldx acc0_hi
                    jsr dump_line
                    jsr newline
                    ; read cursor position
                    lda #$86
                    jsr osbyte
                    cpy ptr0_hi
                    beq L9689
                    sty ptr0_hi
                    bne L9674
L9689               lda #5
                    sta acc0_lo
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    jsr prstr
                    ; move cursor home
                    !text 30, 0
                    ; setup keyboard
L9698               lda #$04
                    ldx #$02
                    jsr osbyte
                    ; function key base code
                    lda #$e1
                    ldx #$7c
                    ldy #0
L96a5               jsr osbyte
                    ; shift function key base code
                    lda #$e2
                    ldx #$94
                    ldy #0
                    jsr osbyte
edit_getch          jsr show_line
                    ; flush input buffers
                    lda #$0f
                    ldx #$01
                    jsr osbyte
                    jsr osrdch
                    bcc edit_dispatch
                    ; ack escape and exit
                    lda #$7e
                    jsr osbyte
                    lda #$04
                    ldx #0
                    jsr osbyte
                    lda #$e1
                    ldx #$01
                    jsr osbyte
                    lda #$e2
                    ldx #$01
                    jsr osbyte
                    jmp newline
                    
                    ; left
edit_dispatch       cmp #$88
                    bne L96fb
                    lda acc0_lo
                    cmp #5
                    beq L96ef
                    cmp cmdjmp_hi
                    beq L96f3
                    dec acc0_lo
                    bne edit_getch
L96ef               lda cmdjmp_lo
                    bne L96f5
L96f3               lda cmdhelp_lo
L96f5               sta acc0_lo
                    ; fake up
                    lda #$8b
                    bne L9731
                    ; right
L96fb               cmp #$89
                    bne L9717
                    lda acc0_lo
                    cmp cmdjmp_lo
                    beq L970d
                    cmp cmdhelp_lo
                    beq L9711
                    inc acc0_lo
                    bne edit_getch
L970d               lda #$05
                    bne L9713
L9711               lda cmdjmp_hi
L9713               sta acc0_lo
                    lda #$8a
                    ; down
L9717               cmp #$8a
                    bne L9731
                    jsr show_line
                    lda #10
                    jsr oswrch
                    lda acc2_lo
                    clc
                    adc acc0_hi
                    sta acc2_lo
                    bcc L974b
                    inc acc2_hi
                    jmp edit_getch
                    
                    ; up
L9731               cmp #$8b
                    bne L974e
                    jsr show_line
                    lda #$0b
                    jsr oswrch
                    lda acc2_lo
                    sec
                    sbc acc0_hi
                    sta acc2_lo
                    bcs L974b
                    dec acc2_hi
                    jmp edit_getch
                    
L974b               jmp edit_getch
                    
                    ; shift right = skip to ascii column
L974e               cmp #$a1
                    bne L976b
                    lda cmdjmp_lo
                    cmp acc0_lo
                    bcc L974b
                    lda acc0_lo
                    sec
                    sbc #$05
                    jsr div3
                    lda $0100
                    clc
                    adc cmdjmp_hi
                    sta acc0_lo
                    jmp edit_getch
                    
                    ; shift left = skip to hex column
L976b               cmp #$a0
                    bne L9785
                    lda acc0_lo
                    cmp cmdjmp_hi
                    bcc L974b
                    lda acc0_lo
                    sec
                    sbc cmdjmp_hi
                    jsr times3
                    clc
                    adc #$05
                    sta acc0_lo
                    jmp edit_getch
                    
                    ; return
L9785               cmp #13
                    bne edit_input
                    ldx #$05
                    lda acc0_lo
                    cmp cmdjmp_hi
                    bcc L9793
                    ldx cmdjmp_hi
L9793               stx acc0_lo
                    ; fake line down. Could just jump to the code after the key test
                    lda #$8a
                    jmp L9717
                    
                    ; text input
edit_input          tax
                    lda acc0_lo
                    cmp cmdjmp_hi
                    bcc edit_hex
                    sbc cmdjmp_hi
                    tay
                    txa
                    sta (acc2_lo), y
                    cmp (acc2_lo), y
                    bne edit_bell
                    ; fake right. Could just jump in after the test
                    lda #$89
                    jmp L96fb
                    
edit_hex            stx $b6
L97b2               sbc #$04
                    jsr div3
                    cmp #$02
                    bne L97ca
                    ; move forward and try again
                    inc acc0_lo
                    lda acc0_lo
                    clc
                    bcc L97b2
edit_bell           lda #$07
                    jsr oswrch
                    jmp edit_getch
                    
L97ca               lda $b6
                    sbc #$2f
                    bcc edit_bell
                    cmp #$0a
                    bcc L97da
                    cmp #$11
                    bcc edit_bell
                    sbc #$07
L97da               cmp #$10
                    bcs edit_bell
                    ldx #$f0
                    ldy $0101
                    bne L97eb
                    ldx #$0f
                    asl
                    asl
                    asl
                    asl
L97eb               stx $0101
                    ldy $0100
                    sta $0100
                    lda (acc2_lo), y
                    and $0101
                    ora $0100
                    sta (acc2_lo), y
                    cmp (acc2_lo), y
                    bne edit_bell
                    ; fake right
                    lda #$89
                    jmp L96fb
                    
L9807               jmp edit_getch
                    
; fix00-                    
layout_40           !hex 1b 1d 24 
layout_80           !hex 33 35 44 
; fix00+
                    
cursor_off          lda ptr0_lo
                    pha
                    lda ptr0_hi
                    pha
                    jsr prstr
                    ; cursor off
                    !text 23, 1, 0
L981c               jmp L982c
                    
cursor_on           lda ptr0_lo
                    pha
                    lda ptr0_hi
                    pha
                    jsr prstr
                    ; cursor on
                    !text 23, 1, 1, 0
L982c               tya
                    ldx #$0a
L982f               jsr oswrch
                    dex
                    bne L982f
                    pla
                    sta ptr0_hi
                    pla
                    sta ptr0_lo
                    rts

; fix00-                    
inst_len_table      !hex 69 69 65 7d a5 69 75 7d e5 a9 65 fd a5 69 75 7d 
                    !hex 65 69 65 fd a5 69 75 7d 65 69 65 fd a5 69 75 7d 
                    !hex 65 a9 55 fd a5 a9 75 75 a9 a9 65 fd a5 a9 75 fd 
                    !hex a5 a9 65 fd a5 69 75 7d a5 a9 65 fd a5 69 75 7d 
; fix00+

inst_len            pha
                    lsr
                    lsr
                    tax
                    pla
                    and #$03
                    tay
                    lda inst_len_table, x
-                   iny
                    cpy #$04
                    beq +
                    lsr
                    lsr
                    jmp -
                    
+                   and #$03
                    rts
                    
                    ; bootstrap for stepper
step_bootstrap      nop
                    nop
                    jmp stepped
                    jmp branched
                    
c_step              jsr check_cmd_mode
                    jsr eval
                    jsr acc0_to_acc2
                    jsr hex_prefix
                    jsr print_or_page
                    ldx reg_s
                    cpx #$80
                    bcs +
                    jsr throw
                    !text $af, "Stack overflow", 0

+                   txs
                    jsr prstr
                    !text "PC", -3, "A", -2, "X", -2, "Y", -2, "P", -8, "S", -2, "Opcode", 13, 0

L98de               sty ptr0_lo
                    sty ptr0_hi
step_next           +inc16 reg_i
                    lda acc2_hi
                    jsr hexbyte
                    lda acc2_lo
                    jsr hexbyte_space
                    tsx
                    stx reg_s
                    lda reg_a
                    jsr hexbyte_space
                    lda reg_x
                    jsr hexbyte_space
                    lda reg_y
                    jsr hexbyte_space
                    lda reg_p
L990d               jsr show_flags
                    lda #' '
                    jsr oswrch
                    lda reg_s
                    jsr hexbyte_space
                    ldx #$07
L991d               lda step_bootstrap, x
                    sta $010c, x
                    dex
                    bpl L991d
                    lda acc2_lo
                    sta acc0_lo
                    lda acc2_hi
                    sta acc0_hi
                    inx
                    stx $b6
                    jsr decode_instr
                    ldx #0
                    stx $b6
                    jsr decode_admode
                    jsr osrdch
                    jsr newline

                    ; JMP, Bxx, RTS etc are emulated
                    lda $010b
                    beq do_brk
                    ; JSR
                    cmp #$20
                    beq do_jsr
                    ; JMP
                    cmp #$4c
                    beq do_jmp
                    ; JMP ()
                    cmp #$6c
                    beq do_jmpi
                    ; RTS
                    cmp #$60
                    beq do_rts
                    ; RTI
                    cmp #$40
                    beq do_rti
                    ; branch?
                    and #$1f
                    cmp #$10
                    beq do_branch
step_execute        php
                    pla
                    sta acc1_lo
                    lda #0
                    sta $b6
                    jsr load_regs
                    jmp $010b
                    
do_brk              jsr throw
                    !text $b0, "Halted", 0
                    
do_jsr              jsr ex_or_step
                    bne step_execute
                    clc
                    lda acc0_lo
                    adc #2
                    tax
                    lda acc0_hi
                    adc #0
                    pha
                    txa
                    pha
                    
do_jmp              jsr next_pc
                    jsr indirect
                    jmp step_next
                    
do_jmpi             jsr next_pc
                    jsr indirect
                    jsr indirect
                    jmp step_next
                    
do_rts              pla
                    adc #0
                    sta acc2_lo
                    pla
                    adc #0
                    sta acc2_hi
                    jmp step_next
                    
do_rti              pla
                    sta reg_p
                    clc
                    bcc do_rts
                    
do_branch           lda $010c
                    sta acc1_hi
                    lda #$04
L99bb               sta $010c
                    jmp step_execute
                    
ex_or_step          jsr prstr
                    !text "Execute", -1, "or", -1, "step", -1, "(E/S)", -1, "?", -1, 0
L99dd               sty ptr0_lo
                    sty ptr0_hi
-                   jsr osrdch
                    jsr check_escape
                    and #$df
                    cmp #'S'
                    beq +
                    cmp #'E'
                    bne -
+                   jsr oswrch
                    cmp #'S'
                    php
                    jsr newline
                    plp
                    rts
                    
indirect            jsr read_rom
                    pha
                    jsr read_rom
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    rts
                    
next_pc             lda acc0_lo
                    clc
                    adc #1
                    sta acc2_lo
                    lda acc0_hi
                    adc #0
                    sta acc2_hi
                    rts
                    
branched            jsr save_regs
                    lda acc1_lo
                    pha
                    plp
                    ldx #0
                    lda acc1_hi
                    bpl +
                    dex
+                   clc
                    adc acc2_lo
                    sta acc2_lo
                    txa
                    adc acc2_hi
                    sta acc2_hi
                    jmp step_next
                    
stepped             jsr save_regs
                    lda acc1_lo
                    pha
                    plp
                    jmp step_next
                    
c_look              jsr eval
                    jsr acc0_to_acc2
                    jsr hex_prefix
                    jsr check_eol
                    ldx cur_rom
                    lda rom_private, x
                    bmi L9a54
                    lda #0
                    tax
                    beq L9a69
L9a54               lda $0600
                    ora $0601
                    tax
                    beq L9a69
                    sec
                    lda $0600
                    sbc acc2_lo
                    tax
                    lda $0601
                    sbc acc2_hi
L9a69               stx ptr0_lo
                    sta ptr0_hi

                    ; cursor edit
                    lda #4
                    ldx #1
                    jsr osbyte
L9a74               sec
                    lda acc2_lo
                    sta acc0_lo
                    sbc #$28
                    sta acc2_lo
                    lda acc2_hi
                    sta acc0_hi
                    sbc #0
                    sta acc2_hi
L9a85               lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    jsr read_rom
                    jsr inst_len
                    sec
                    sbc #$01
                    clc
                    adc acc2_lo
                    sta acc2_lo
                    bcc L9a9d
                    inc acc2_hi
L9a9d               lda acc2_lo
                    cmp acc0_lo
                    lda acc2_hi
                    sbc acc0_hi
                    bcc L9b00
L9aa7               jsr osrdch
                    bcc L9ab6
                    lda #4
                    ldx #0
                    jsr osbyte
                    jmp throw_escape
                    
L9ab6               cmp #$8a
                    beq L9acf
                    cmp #$8b
                    bne L9aa7
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    lda #$0b
                    jsr oswrch
                    jsr oswrch
                    jmp L9ae4
                    
L9acf               pla
                    pla
                    jsr read_rom
                    jsr inst_len
                    sec
                    sbc #$01
                    clc
                    clc
                    adc acc2_lo
                    sta acc2_lo
                    bcc L9ae4
                    inc acc2_hi
L9ae4               lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    ldx #0
                    stx $b6
                    jsr cursor_off
                    jsr dis_op
                    jsr cursor_on
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    jmp L9a74
                    
L9b00               pla
                    pla
                    jmp L9a85
                    
parse_tracks        jsr skip_space
                    ldx #$28
                    cmp #$34
                    beq L9b28
                    ldx #$50
                    cmp #$38
                    beq L9b28
L9b14               jsr throw
                    !text $b1, "40 or 80 tracks", 0
L9b28               jsr getch
                    cmp #$30
                    bne L9b14
                    txa
                    pha
                    jsr skip_comma
                    pla
                    rts
                    
parse_drive         jsr skip_space
                    pha
                    jsr unget
                    pla
                    cmp #$0d
                    beq L9b4d
                    jsr eval_byte
                    cmp #$04
                    bcc L9b4c
                    jmp throw_baddrive
                    
L9b4c               rts
                    
L9b4d               lda #$01
                    sta $0100
                    lda #$82
                    jsr osbyte
                    stx $0103
                    sty $0104
                    lda #$10
                    sta $0101
                    ldy #$01
                    sty $0102
                    lda #$06
                    ldx #0
                    lda #$06
                    ldx #0
                    stx $0105
                    stx $0106
                    stx $0107
                    stx $0108
                    lda acc2_lo
                    pha
                    lda acc2_hi
                    pha
                    jsr osgbpb
                    pla
                    sta acc2_hi
                    pla
                    sta acc2_lo
                    lda $0111
                    and #$03
                    rts

; fix00-                    
empty_disc          !hex 00 ff ff ff ff 01 69 00 00 00 ff ff ff ff 05 63 
                    !hex 00 10 2a 00 10 00 00 00 19 ff ff 03 5f 00 00 2a 
                    !hex 00 00 ff ff ff ff 04 4b 00 00 22 00 
; fix00+
L9bbc               pla
                    rts
                    
c_form              jsr parse_tracks
                    sta acc2_lo
                    sta acc2_hi
                    jsr parse_drive
                    pha
                    jsr check_eol
                    lda #$83
                    jsr osbyte
                    sty acc0_hi
                    stx acc0_lo
                    tya
                    tax
                    inx
                    inx
                    inx
                    jsr ram_warning
                    bcc L9bbc
                    ldy #$2b
L9be1               lda empty_disc, y
L9be4               sta (acc0_lo), y
                    dey
                    bpl L9be1
                    jsr disc_block                    
                    pla
                    ldy #0
                    sta (acc0_lo), y
                    ldy #$09
                    sta (acc0_lo), y
                    ldy #$16
                    sta (acc0_lo), y
                    ldy #$21
                    sta (acc0_lo), y
                    ldx acc0_lo
                    ldy acc0_hi
                    lda #$7f
                    jsr osword
                    lda #0
                    sta acc1_lo
                    sta acc1_hi
L9c0c               ldy #$03
L9c0e               jsr wrap_sector
                    dey
                    bne L9c0e
                    lda acc1_lo
                    jsr hexbyte
                    jsr prstr
                    !text -2, 0
L9c1e               ldy #$59
                    ldx #$0a
L9c22               lda #$01
                    sta (acc0_lo), y
                    dey
                    txa
                    pha
                    jsr wrap_sector
                    sta (acc0_lo), y
                    dey
                    pla
                    tax
                    lda #0
                    sta (acc0_lo), y
                    dey
                    lda acc1_lo
                    sta (acc0_lo), y
                    dey
                    dex
                    bne L9c22
                    ldy #$0a
                    lda acc0_lo
                    clc
                    adc #$32
                    sta (acc0_lo), y
                    iny
                    lda acc0_hi
                    adc #0
                    sta (acc0_lo), y
                    ldy #$10
                    lda acc1_lo
                    sta (acc0_lo), y
                    lda acc0_lo
                    clc
                    adc #$09
                    tax
                    lda acc0_hi
                    adc #0
                    tay
                    lda #$7f
                    jsr osword
                    ldy #$15
                    lda (acc0_lo), y
                    beq L9c7b
                    jsr throw
                    ; "Format error"
                    !text $b2, "Format error", 0
L9c7b               ldy #$1d
                    lda acc1_lo
                    sta (acc0_lo), y
                    lda acc0_lo
                    clc
                    adc #$16
                    tax
                    lda acc0_hi
                    adc #0
                    tay
                    lda #$7f
                    jsr osword
                    ldy #$20
                    lda (acc0_lo), y
                    beq L9ca8
                    jsr throw
                    ; "Verify error"
                    !text $b3, "Verify error", 0
L9ca8               inc acc1_lo
                    lda acc1_lo
                    cmp acc2_lo
                    bcs L9cb3
                    jmp L9c0c
                    
L9cb3               ldy acc0_lo
                    sty ptr0_lo
                    ldx acc0_hi
                    inx
                    stx ptr0_hi
                    ldy #0
                    tya
L9cbf               sta (ptr0_lo), y
                    iny
                    bne L9cbf
                    inc ptr0_hi
L9cc6               sta (ptr0_lo), y
                    iny
                    bne L9cc6
                    sty acc2_hi
                    lda acc2_lo
                    asl
                    pha
                    asl
                    rol acc2_hi
                    asl
                    rol acc2_hi
                    sta acc2_lo
                    pla
                    adc acc2_lo
                    sta acc2_lo
                    bcc L9ce2
                    inc acc2_hi
L9ce2               ldy #$07
                    lda acc2_lo
                    sta (ptr0_lo), y
                    dey
                    lda acc2_hi
                    sta (ptr0_lo), y
                    ldy #$22
                    lda ptr0_lo
                    sta (acc0_lo), y
                    iny
                    ldx ptr0_hi
                    dex
                    txa
                    sta (acc0_lo), y
                    lda acc0_lo
                    clc
                    adc #$21
                    tax
                    lda acc0_hi
                    adc #0
                    tay
                    lda #$7f
                    jmp osword
                    
disc_block          lda #$82
                    jsr osbyte
                    stx ptr0_lo
                    sty ptr0_hi
                    ldx #0
L9d15               ldy disc_offsets, x
                    lda ptr0_lo
                    sta (acc0_lo), y
                    iny
                    lda ptr0_hi
                    sta (acc0_lo), y
                    inx
                    cpx #$04
                    bcc L9d15
                    rts

; fix00-                    
disc_offsets        !hex 03 0c 19 24 
; fix00+
wrap_sector         ldx acc1_hi
                    dex
                    bpl +
                    ldx #9
+                   txa
                    stx acc1_hi
                    rts
                    
parse_track_drive   lda #0
                    sta acc0_hi
                    sta acc2_lo
                    jsr eval_byte
                    sta acc0_lo
                    jsr skip_comma
                    jsr parse_drive
                    sta acc2_lo
                    jsr check_eol
                    lda acc2_lo
                    ldx #$2a
                    rts
                    
c_get               jsr parse_track_drive
                    ldy #$53
                    bne L9d5d
c_put               jsr parse_track_drive
                    ldy #$4b
L9d5d               jsr get_put_parms
                    jsr osword
                    jsr prstr
                    !text "I/O", -1, "result", -1, "=", -1, 0

L9d74               lda $010a
                    sta acc0_lo
                    sty acc0_hi
                    ldx #0
                    lda #10
                    jsr radix_out
                    jmp newline
                    
c_mode              jsr eval_byte
                    tax
                    jsr check_eol
                    cpx #8
                    bcc +
                    jsr throw
                    !text 25, "Bad MODE", 0
+                   lda #22
                    jsr oswrch
                    txa
                    jmp oswrch
                    
c_vdu               ldx #0
                    stx ptr0_lo
                    inx
                    stx ptr0_hi
                    jsr parse_bytes
-                   dex
                    bmi +
                    lda $0100, y
                    jsr oswrch
                    iny
                    bne -
+                   rts
                    
c_same              jsr eval
                    jsr acc0_to_acc2
                    jsr eval
                    lda acc0_lo
                    sta $b6
                    lda acc0_hi
                    sta $b7
                    jsr eval
                    jsr check_eol
L9dd4               lda ($b6), y
                    cmp (acc2_lo), y
                    beq L9de7
                    ldx #$b4
                    jsr pr_membyte
                    ldx #$b6
                    jsr pr_membyte
                    jsr newline
L9de7               iny
                    bne L9dee
                    inc $b7
                    inc acc2_hi
L9dee               lda acc0_lo
                    bne L9df4
                    dec acc0_hi
L9df4               dec acc0_lo
                    lda acc0_lo
                    ora acc0_hi
                    bne L9dd4
                    rts
                    
pr_membyte          lda #$28
                    jsr oswrch
                    clc
                    tya
                    pha
                    adc 0, x
                    sta ptr0_lo
                    lda 1, x
                    adc #0
                    sta ptr0_hi
                    jsr hexbyte
                    lda ptr0_lo
                    jsr hexbyte
                    lda #$29
                    jsr oswrch
                    lda #$3d
                    jsr oswrch
                    ldy #0
                    lda (ptr0_lo), y
                    jsr hexbyte_space
                    pla
                    tay
                    rts
                    
pr_sym16            tya
                    pha
                    txa
                    pha
                    lda 0
                    pha
                    lda $01
                    pha
                    lda $02
                    pha
                    lda $03
                    pha
                    lda #< symtab
                    sta 0
                    lda #> symtab
                    sta $01
                    jsr read_rom
                    sta $02
                    jsr read_rom
                    sta $03
                    ldy #0
L9e4f               lda (0), y
                    cmp $02
                    bne L9e6b
                    iny
                    lda (0), y
                    cmp $03
                    bne L9e6c
                    iny
L9e5d               lda (0), y
                    pha
                    iny
                    and #$7f
                    jsr oswrch
                    pla
                    bpl L9e5d
                    bmi L9e94
L9e6b               iny
L9e6c               iny
                    lda (0), y
                    bpl L9e6c
                    iny
                    tya
                    clc
                    adc 0
                    sta 0
                    bcc L9e7c
                    inc $01
L9e7c               ldy #$01
                    lda (0), y
                    dey
                    ora (0), y
                    bne L9e4f
                    lda $b8
                    jsr oswrch
                    lda $03
                    jsr hexbyte
                    lda $02
                    jsr hexbyte
L9e94               pla
                    sta $03
                    pla
                    sta $02
                    pla
                    sta $01
                    pla
                    sta 0
                    pla
                    tax
                    pla
                    tay
                    rts
                    
symtab              !word oswrch 
                    !text "OSWRC", 'H' + $80
                    !word osrdch 
                    !text "OSRDC", 'H' + $80
                    !word osnewl 
                    !text "OSNEW", 'L' + $80
                    !word osasci 
                    !text "OSASC", 'I' + $80
                    !word $ffc2 
                    !text "GSINI", 'T' + $80
                    !word $ffc5 
                    !text "GSREA", 'D' + $80
                    !word $ffb9 
                    !text "OSRDR", 'M' + $80
                    !word $ffbf 
                    !text "OSEVE", 'N' + $80
                    !word oscli 
                    !text "OSCL", 'I' + $80
                    !word osbyte 
                    !text "OSBYT", 'E' + $80
                    !word osword 
                    !text "OSWOR", 'D' + $80
                    !word osfind 
                    !text "OSFIN", 'D' + $80
                    !word osgbpb 
                    !text "OSGBP", 'B' + $80
                    !word osbput 
                    !text "OSBPU", 'T' + $80
                    !word osbget 
                    !text "OSBGE", 'T' + $80
                    !word osargs 
                    !text "OSARG", 'S' + $80
                    !word osfile 
                    !text "OSFIL", 'E' + $80
                    !word brkv 
                    !text "brk", 'v' + $80
                    !word $0203 
                    !text "brkv+", '1' + $80
                    !word wrchv 
                    !text "wrch", 'v' + $80
                    !word wrchv + 1
                    !text "wrchv+", '1' + $80
                    !word rdchv 
                    !text "rdch", 'v' + $80
                    !word rdchv + 1
                    !text "rdchv+", '1' + $80
                    !word $0100 
                    !text "stac", 'k' + $80
                    !word L8000 
                    !text "lan", 'g' + $80
                    !word L8003 
                    !text "serv", 'e' + $80
                    !word 0 

credits             !text "(C) Andrew Armstrong 1984. Thanks to Nigel, Joe and all at HCCS."
                    !text "Version 1.60 12.2.1984"

                    ; Pad with spaces to next 8k boundary
                    !align $1fff, 0, ' '